plugin_option(PLUGIN_KAFKA "Kafka stats plugin" ON)

set(BUILD_PLUGIN_KAFKA 0)

if(PLUGIN_KAFKA)
    find_package(LibRdkafka)
    if (LIBRDKAFKA_FOUND)
        set(BUILD_PLUGIN_KAFKA 1)
    else()
        set(BUILD_PLUGIN_KAFKA_REASON "librdkafka not found" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_KAFKA ${BUILD_PLUGIN_KAFKA} PARENT_SCOPE)

if(BUILD_PLUGIN_KAFKA)
    set(PLUGIN_KAFKA_SRC kafka.c)
    add_library(kafka MODULE ${PLUGIN_KAFKA_SRC})
    target_link_libraries(kafka PRIVATE libmetric libutils LibRdkafka::LibRdkafka)
    if(LIBRDKAFKA_HAVE_LIBRDKAFKA_LOG_CB)
        target_compile_definitions(kafka PUBLIC HAVE_LIBRDKAFKA_LOG_CB)
    endif()
    if(LIBRDKAFKA_HAVE_LIBRDKAFKA_LOGGER)
        target_compile_definitions(kafka PUBLIC HAVE_LIBRDKAFKA_LOGGER)
    endif()
    set_target_properties(kafka PROPERTIES PREFIX "")
    install(TARGETS kafka DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-kafka.5 ncollectd-kafka.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-kafka.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
