plugin_option(PLUGIN_SSSD "SSSD plugin" ON)

set(BUILD_PLUGIN_SSSD 0)

if(PLUGIN_SSSD)
    if(BUILD_LINUX)
        check_include_file(linux/config.h HAVE_LINUX_CONFIG_H)
        find_package(LibSystemd)
        if (LIBSYSTEMD_FOUND)
            check_include_file(systemd/sd-bus.h HAVE_SSSD_SD_BUS_H)
            if (HAVE_SSSD_SD_BUS_H)
                set(BUILD_PLUGIN_SSSD 1)
            else()
                set(BUILD_PLUGIN_SSSD_REASON "systemd/sd-bus.h not found" PARENT_SCOPE)
            endif()
        else()
            set(BUILD_PLUGIN_SSSD_REASON "libsystemd not found" PARENT_SCOPE)
        endif()
    else()
        set(BUILD_PLUGIN_SSSD_REASON "Linux only" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_SSSD ${BUILD_PLUGIN_SSSD} PARENT_SCOPE)

if(BUILD_PLUGIN_SSSD)
    set(PLUGIN_SSSD_SRC sssd.c)
    add_library(sssd MODULE ${PLUGIN_SSSD_SRC})
    target_link_libraries(sssd PRIVATE libmetric libutils LibSystemd::LibSystemd)
    set_target_properties(sssd PROPERTIES PREFIX "")
    if(HAVE_LINUX_CONFIG_H)
        target_compile_definitions(sssd PUBLIC HAVE_LINUX_CONFIG_H)
    endif()
    install(TARGETS sssd DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-sssd.5 ncollectd-sssd.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-sssd.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
