plugin_option(PLUGIN_WRITE_KAFKA "Kafka output plugin" ON)

set(BUILD_PLUGIN_WRITE_KAFKA 0)

if(PLUGIN_WRITE_KAFKA)
    find_package(LibRdkafka)
    if (LIBRDKAFKA_FOUND)
        set(BUILD_PLUGIN_WRITE_KAFKA 1)
    else()
        set(BUILD_PLUGIN_WRITE_KAFKA_REASON "librdkafka not found" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_WRITE_KAFKA ${BUILD_PLUGIN_WRITE_KAFKA} PARENT_SCOPE)

if(BUILD_PLUGIN_WRITE_KAFKA)
    set(PLUGIN_WRITE_KAFKA_SRC write_kafka.c)
    add_library(write_kafka MODULE ${PLUGIN_WRITE_KAFKA_SRC})
    target_link_libraries(write_kafka PRIVATE libmetric libutils libxson libformat LibRdkafka::LibRdkafka)
    if(LIBRDKAFKA_HAVE_LIBRDKAFKA_LOG_CB)
        target_compile_definitions(write_kafka PUBLIC HAVE_LIBRDKAFKA_LOG_CB)
    endif()
    if(LIBRDKAFKA_HAVE_LIBRDKAFKA_LOGGER)
        target_compile_definitions(write_kafka PUBLIC HAVE_LIBRDKAFKA_LOGGER)
    endif()
    set_target_properties(write_kafka PROPERTIES PREFIX "")
    install(TARGETS write_kafka DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-write_kafka.5 ncollectd-write_kafka.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-write_kafka.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
