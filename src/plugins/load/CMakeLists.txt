plugin_option(PLUGIN_LOAD "System load" ON)

include(CheckFunctionExists)
include(CheckIncludeFile)
include(CheckIncludeFiles)

set(BUILD_PLUGIN_LOAD 0)

if(PLUGIN_LOAD)
    check_function_exists(getloadavg HAVE_GETLOADAVG)
    if(HAVE_GETLOADAVG)
        if(HAVE_SYS_TIME_H)
            check_include_files("sys/time.h;sys/loadavg.h" HAVE_SYS_LOADAVG_H)
        else()
            check_include_file(sys/loadavg.h HAVE_SYS_LOADAVG_H)
        endif()
    endif()

    if(BUILD_LINUX)
        set(BUILD_PLUGIN_LOAD 1)
    elseif(BUILD_AIX)
        find_package(LibPerfstat)
        if(LIBPERFSTAT_FOUND)
            set(BUILD_PLUGIN_LOAD 1)
        else()
            set(BUILD_PLUGIN_LOAD_REASON "perfstat not found" PARENT_SCOPE)
        endif()
    elseif(HAVE_GETLOADAVG)
        set(BUILD_PLUGIN_LOAD 1)
    else()
        set(BUILD_PLUGIN_LOAD_REASON "unsupported system" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_LOAD ${BUILD_PLUGIN_LOAD} PARENT_SCOPE)

if(BUILD_PLUGIN_LOAD)
    set(PLUGIN_LOAD_SRC load.c)
    set(PLUGIN_LOAD_LIBS libmetric libutils)

    if(HAVE_GETLOADAVG)
        list(APPEND PLUGIN_LOAD_DEFINITIONS HAVE_GETLOADAVG)
    endif()
    if(HAVE_SYS_LOADAVG_H)
        list(APPEND PLUGIN_LOAD_DEFINITIONS HAVE_SYS_LOADAVG_H)
    endif()
    if(BUILD_AIX)
        list(APPEND PLUGIN_LOAD_LIBS LibPerfstat::LibPerfstat)
    endif()

    add_library(load MODULE ${PLUGIN_LOAD_SRC})
    target_compile_definitions(load PUBLIC ${PLUGIN_LOAD_DEFINITIONS})
    target_link_libraries(load PRIVATE ${PLUGIN_LOAD_LIBS})
    set_target_properties(load PROPERTIES PREFIX "")

    if(BUILD_LINUX)
        add_executable(test_plugin_load EXCLUDE_FROM_ALL load_linux_test.c ${PLUGIN_LOAD_SRC})
        target_link_libraries(test_plugin_load libtest libmetric libutils -lm)
        add_dependencies(build_tests test_plugin_load)
        add_test(NAME test_plugin_load COMMAND test_plugin_load WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})
    endif()

    install(TARGETS load DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-load.5 ncollectd-load.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-load.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
