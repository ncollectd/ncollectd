plugin_option(PLUGIN_CONTEXTSWITCH "Context switch statistics" ON)

set(BUILD_PLUGIN_CONTEXTSWITCH 0)

if(PLUGIN_CONTEXTSWITCH)
    if(BUILD_LINUX)
        set(BUILD_PLUGIN_CONTEXTSWITCH 1)
    elseif(BUILD_BSD)
       if(HAVE_SYSCTLBYNAME)
            if(HAVE_SYS_SYSCTL_H)
                set(BUILD_PLUGIN_CONTEXTSWITCH 1)
            else()
                set(BUILD_PLUGIN_CONTEXTSWITCH_REASON "sys/sysclt.h not found" PARENT_SCOPE)
            endif()
         else()
            set(BUILD_PLUGIN_CONTEXTSWITCH_REASON "sysctlbyname not found" PARENT_SCOPE)
         endif()
    elseif(BUILD_AIX)
        find_package(LibPerfstat)
        if(LIBPERFSTAT_FOUND)
            set(BUILD_PLUGIN_CONTEXTSWITCH 1)
        else()
            set(BUILD_PLUGIN_CONTEXTSWITCH_REASON "perfstat not found" PARENT_SCOPE)
        endif()
    else()
        set(BUILD_PLUGIN_CONTEXTSWITCH_REASON "unsupported system" PARENT_SCOPE)
    endif()
endif()

set(BUILD_PLUGIN_CONTEXTSWITCH ${BUILD_PLUGIN_CONTEXTSWITCH} PARENT_SCOPE)

if(BUILD_PLUGIN_CONTEXTSWITCH)
    set(PLUGIN_CONTEXTSWITCH_SRC contextswitch.c)
    set(PLUGIN_CONTEXTSWITCH_LIBS libmetric libutils)

    if(BUILD_AIX)
        list(APPEND PLUGIN_CONTEXTSWITCH_LIBS LibPerfstat::LibPerfstat)
    endif()

    add_library(contextswitch MODULE ${PLUGIN_CONTEXTSWITCH_SRC})
    target_link_libraries(contextswitch PRIVATE ${PLUGIN_CONTEXTSWITCH_LIBS})
    set_target_properties(contextswitch PROPERTIES PREFIX "")

    if(BUILD_LINUX)
        add_executable(test_plugin_contextswitch EXCLUDE_FROM_ALL contextswitch_linux_test.c ${PLUGIN_CONTEXTSWITCH_SRC})
        target_link_libraries(test_plugin_contextswitch libtest libmetric libutils m)
        add_dependencies(build_tests test_plugin_contextswitch)
        add_test(NAME test_plugin_contextswitch COMMAND test_plugin_contextswitch WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})
    endif()

    install(TARGETS contextswitch DESTINATION ${CMAKE_INSTALL_LIBDIR}/ncollectd/)
    configure_file(ncollectd-contextswitch.5 ncollectd-contextswitch.5 @ONLY)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ncollectd-contextswitch.5 DESTINATION ${CMAKE_INSTALL_MANDIR}/man5)
endif()
